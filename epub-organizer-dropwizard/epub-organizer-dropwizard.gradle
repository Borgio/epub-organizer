apply plugin: 'java'
apply plugin: 'application'

// Set our project variables
project.ext {
    dropwizardVersion = '0.6.2'
}

// The main class of the application
mainClassName = 'nl.jworks.epub.dropwizard.HelloWorldService'

// Use Java 7 by default
sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile project(":epub-organizer-core")

    compile (
            'com.yammer.dropwizard:dropwizard-core:' + dropwizardVersion,
            'com.yammer.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
            'com.yammer.dropwizard:dropwizard-migrations:' + dropwizardVersion,
            'com.yammer.dropwizard:dropwizard-auth:' + dropwizardVersion,
            'com.h2database:h2:1.3.168'
    )
}

// Configure the run task to start the Dropwizard service
run {
    args 'server', './src/main/resources/hello-world.yml'
}

jar {

    manifest {
        attributes "Implementation-Title": "Gradle Quickstart"
        attributes "Implementation-Version": version
        attributes "Main-Class" : mainClassName
    }

    // remove the security files (from mail.jar / activation.jar) so that the jar will be executable.
    doFirst {
        from (configurations.runtime.resolve().collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude 'META-INF/MANIFEST.MF'
            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.DSA'
            exclude 'META-INF/*.RSA'
        }
    }

    doLast {
        println "Jar has been created in ${archivePath}"
    }
}

/**
 * Experimental tasks
 */
task artifacts {
    group = "Help"
    description = "Displays the artifacts associated with each configuration of " + project
    doFirst {
        configurations.findAll().each { config ->
            println "${config}:"
            config.allArtifacts.getFiles().each { file -> println "  " + file}
            println ' '
        }
    }
}

task show << {
    println "-----------------------------------"
    println "Loading build.gradle configuration..."
    println ""
    println "   _____               _ _          "
    println "  / ____|             | | |         "
    println " | |  __ _ __ __ _  __| | | ___     "
    println " | | |_ | '__/ _` |/ _` | |/ _ \\    "
    println " | |__| | | | (_| | (_| | |  __/    "
    println "  \\_____|_|  \\__,_|\\__,_|_|\\___|    "
    println ""
    println "Build output: " + relativePath(compileJava.destinationDir)
    println "Resources output: " + relativePath(processResources.destinationDir)
    println "-----------------------------------"
}

