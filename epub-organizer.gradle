subprojects {
    apply plugin: 'java'
    group = 'nl.jworks.epub-organizer'
    version = '1.0'

    // Use Java 7 by default
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()

        maven {
            url "https://github.com/psiegman/mvn-repo/raw/master/releases"
        }
    }

    task initProject() << {
        if (hasProperty(initPlugins)) {
            initPlugins.split(',').each { plug ->
                project.apply {
                    plugin(plug.trim())
                }
            }
        }

        project.sourceSets*.allSource.srcDirTrees.flatten().dir.each { dir ->
            dir.mkdirs()
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.+'
        testCompile 'org.easymock:easymock:3.1'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

def artifacts = []

gradle.addListener(new TaskExecutionListener() {
        void afterExecute(Task task, TaskState state) {

                if(task in AbstractArchiveTask) {
                        artifacts << task.outputs.files.singleFile
                }
        }

        void beforeExecute(Task task) { }
})

gradle.addBuildListener(new BuildAdapter() {
        void buildFinished(BuildResult result) {
                if(artifacts) {
                        println "\nOutput location: ${artifacts.last()}\n"
                }
        }
})

